VERSION 1.0 CLASS
BEGIN
  MultiUse = -1  'True
END
Attribute VB_Name = "clsIterator"
Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = False
Attribute VB_PredeclaredId = False
Attribute VB_Exposed = False
Option Explicit

'************************************************************************************
' 機　能    :Iteratorクラス
' Ver       :1.0.00
' -----------------------------------------------------------------------------------
' 履　歴　　:2020/04/10  J.Tamura  Ver.1.0.00  新規作成
'************************************************************************************

'設定配列
Private pIter As Variant

'要素番号（カーソル位置）
Private pNumCursor As Long

'要素番号（バイアス）
Private pNumBias As Long

'************************************************************************************
' 機　能    :コンストラクタ
' -----------------------------------------------------------------------------------
' 履　歴　　:2020/04/10  J.Tamura  Ver.1.0.00  新規作成
'************************************************************************************
Private Sub Class_Initialize()
    pNumCursor = 0
    pNumBias = 0
End Sub
'************************************************************************************
' 機　能    :デストラクタ
' -----------------------------------------------------------------------------------
' 履　歴　　:2020/04/10  J.Tamura  Ver.1.0.00  新規作成
'************************************************************************************
Private Sub Class_Terminate()
    If Not IsEmpty(pIter) Then
        Erase pIter
    End If
    pNumCursor = 0
    pNumBias = 0
End Sub

'************************************************************************************
' 機　能    :セッター
' -----------------------------------------------------------------------------------
' 履　歴　　:2020/04/10  J.Tamura  Ver.1.0.00  新規作成
'************************************************************************************
Public Property Let setAry(ByVal elem As Variant)
    
    '配列を取得
    pIter = elem
    
    '最小要素番号を取得
    pNumBias = LBound(pIter)

    'カーソル位置を最小要素番号で初期化
    pNumCursor = pNumBias - 1

End Property

'************************************************************************************
' 機　能    :次の要素が存在するか
' 説  明    :要素番号を要素数で割った余りの値から、次の要素が存在するか判断する
' -----------------------------------------------------------------------------------
' 履　歴　　:2020/04/10  J.Tamura  Ver.1.0.00  新規作成
'************************************************************************************
Public Function hasNext() As Boolean

    '戻り値をクリア
    hasNext = False

    '配列が空である場合
    If IsEmpty(pIter) Then
        Exit Function
    End If

    '次の要素番号を最大要素番号で割った余りが0である場合
    If (pNumCursor + 1) Mod (UBound(pIter) + 1) = 0 Then

        '次の要素が存在しない
        hasNext = False
        Exit Function

    End If
    
    '次の要素が存在する
    hasNext = True

End Function

'************************************************************************************
' 機　能    :次の要素を設定する
' -----------------------------------------------------------------------------------
' 履　歴　　:2020/04/10  J.Tamura  Ver.1.0.00  新規作成
'************************************************************************************
Public Function toNext() As Variant

    '次の要素が存在する場合は次の要素を設定
    '次の要素が存在しない場合は最小の要素を設定
    pNumCursor = ((pNumCursor + 1 - pNumBias) Mod (UBound(pIter) + 1 - pNumBias)) + pNumBias

    '次の要素を設定
    If IsObject(pIter(pNumCursor)) Then
        
        'オブジェクト型である場合
        Set toNext = pIter(pNumCursor)
    Else
    
        'データ型である場合
        toNext = pIter(pNumCursor)
    End If

End Function
